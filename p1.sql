
/*
 * RESPOSTAS DA PROVA I DE BD2A3
 * ESTE SCRIPT NAO FAZ PARTE DA ATIVIDADE
 *
 * ESTA PROVA FOI REALIZADA ORIGINALMENTE NO DIA
 * 24/09/2019 E CONSISTIA EM RESOLVER SETE QUESTOES
 * RELACIONADAS A UM BANCO DE DADOS FORNECIDO PARA TAL
 *
 * INDIVIDUAL E SEM CONSULTA, FOI REALIZADA NO SQL
 * SERVER, DA MICROSOFT, EM UM COMPUTADOR SEM INTERNET
 *
 * COMO TREINAMENTO, REFIZ ESTA PROVA EM 2020 E
 * DISPONIBILIZEI MINHAS RESPOSTAS NESTE REPOSITORIO
 *
 * RODRIGO GASPERIN, 2020
 */

-- USA BASE DE DADOS 'VET_PROVA'
----------------------------------------------------------------------------------------------------
USE VET_PROVA
GO

-- SELECIONA TODAS AS TABELAS
----------------------------------------------------------------------------------------------------
SELECT * FROM VET_ANIMAL
SELECT * FROM VET_CONSULTA
SELECT * FROM VET_ESTADO
SELECT * FROM VET_HOSPITAL
SELECT * FROM VET_MUNICIPIO
SELECT * FROM VET_PAIS
SELECT * FROM VET_PROPRIEDADEANIMAL
SELECT * FROM VET_PROPRIETARIO
SELECT * FROM VET_RECEITA
SELECT * FROM VET_REMEDIO
SELECT * FROM VET_TIPO
SELECT * FROM VET_VETERINARIO
GO

-- EXERCICIO 1
----------------------------------------------------------------------------------------------------
-- DADOS DOS ANIMAIS COM NOME INICIADO EM 'LA' OU PESO ENTRE 0 E 80:
SELECT A.ANI_ST_NOME AS Nome, A.ANI_RE_PESO AS Peso, A.ANI_RE_ALTURA AS Altura, T.TIP_ST_DESCRICAO AS Tipo
FROM VET_ANIMAL A JOIN VET_TIPO T ON A.TIP_IN_CODIGO = T.TIP_IN_CODIGO
WHERE A.ANI_ST_NOME LIKE 'LA%' OR A.ANI_RE_PESO BETWEEN 0 AND 80
ORDER BY A.ANI_ST_NOME
GO

-- EXERCICIO 2
----------------------------------------------------------------------------------------------------
-- NUMERO DE PROPRIETARIOS DE CADA ANIMAL:
SELECT A.ANI_ST_NOME AS [Nome Animal], COUNT(PA.PRO_IN_CODIGO) AS [Num. Proprietarios]
FROM VET_ANIMAL A JOIN VET_PROPRIEDADEANIMAL PA ON A.ANI_IN_CODIGO = PA.ANI_IN_CODIGO
GROUP BY A.ANI_ST_NOME
HAVING COUNT(PA.PRO_IN_CODIGO) > 1 -- APENAS ANIMAIS COM MAIS DE UM PROPRIETARIO
GO

-- EXERCICIO 3
----------------------------------------------------------------------------------------------------
-- NOME DO PAIS, ESTADO E MUNICIPIO DOS PROPRIETARIOS QUE RECEITARAM 'MERCURIO' AO SEU ANIMAL:
SELECT DISTINCT P.PAI_ST_NOME AS Pais, E.EST_ST_NOME AS Estado, M.MUN_ST_NOME AS Municipio
FROM VET_PROPRIETARIO PR
JOIN VET_MUNICIPIO M ON PR.MUN_IN_CODIGO = M.MUN_IN_CODIGO
JOIN VET_ESTADO E ON M.EST_CH_SIGLA = E.EST_CH_SIGLA
JOIN VET_PAIS P ON E.PAI_ST_SIGLA = P.PAI_ST_SIGLA
JOIN VET_PROPRIEDADEANIMAL PP ON PR.PRO_IN_CODIGO = PP.PRO_IN_CODIGO
JOIN VET_ANIMAL A ON PP.ANI_IN_CODIGO = A.ANI_IN_CODIGO
JOIN VET_CONSULTA C ON A.ANI_IN_CODIGO = C.ANI_IN_CODIGO
JOIN VET_RECEITA R ON C.CON_IN_ID = R.CON_IN_ID
JOIN VET_REMEDIO RM ON R.REM_IN_CODIGO = RM.REM_IN_CODIGO
WHERE RM.REM_ST_NOME = 'MERCURIO'
GO

-- EXERCICIO 4
----------------------------------------------------------------------------------------------------
-- PROCEDURE QUE RETORNA O TOTAL DE CONSULTAS APLICADAS POR TIPO DE ANIMAL:
CREATE PROCEDURE PC_TOTAL_CONSULTAS @ID_TIPO INT
AS BEGIN
	DECLARE @TIPO VARCHAR(50), @TOT INT

	SELECT @TIPO = TIP_ST_DESCRICAO FROM VET_TIPO WHERE TIP_IN_CODIGO = @ID_TIPO

	SELECT @TOT = COUNT(*) FROM VET_TIPO T
		JOIN VET_ANIMAL A ON T.TIP_IN_CODIGO = A.TIP_IN_CODIGO
		JOIN VET_CONSULTA C ON A.ANI_IN_CODIGO = C.ANI_IN_CODIGO
		WHERE T.TIP_IN_CODIGO = @ID_TIPO

	PRINT 'O total de consultas para o tipo ' + @TIPO + ' é ' + CAST(@TOT AS VARCHAR)
END
GO

-- TEMOS APENAS TRES TIPOS DE ANIMAIS:
EXEC PC_TOTAL_CONSULTAS 1
EXEC PC_TOTAL_CONSULTAS 2
EXEC PC_TOTAL_CONSULTAS 3
GO

-- EXERCICIO 5
----------------------------------------------------------------------------------------------------
-- VIEW PARA ARMAZENAR O NOME, O HOSPITAL E O TOTAL DE CONSULTAS DE CADA VETERINARIO:
CREATE VIEW VW_VETERINARIOS
AS
	SELECT V.VET_ST_NOME AS VET_NOME, H.HOS_ST_NOME AS HOS_NOME, COUNT(C.VET_IN_CODIGO) AS TOT_CONSULTAS
	FROM VET_VETERINARIO V
	JOIN VET_HOSPITAL H ON V.HOS_IN_CODIGO = H.HOS_IN_CODIGO
	JOIN VET_CONSULTA C ON V.VET_IN_CODIGO = C.VET_IN_CODIGO
	GROUP BY V.VET_ST_NOME, H.HOS_ST_NOME
GO

-- NOME E HOSPITAL DOS VETERINARIOS COM MAIS DE UMA CONSULTA:
SELECT VET_NOME AS [Nome Veterinario], HOS_NOME AS [Nome Hospital]
FROM VW_VETERINARIOS
WHERE TOT_CONSULTAS > 1
GO

-- EXERCICIO 6
----------------------------------------------------------------------------------------------------
-- VETERINARIO COM O MAIOR NUMERO DE CONSULTAS:
SELECT VET_NOME AS Nome
FROM VW_VETERINARIOS
WHERE TOT_CONSULTAS =
	(SELECT MAX(TOT_CONSULTAS) FROM VW_VETERINARIOS)
GO

-- EXERCICIO 7
----------------------------------------------------------------------------------------------------
-- VIEW PARA ARMAZENAR O NOME E O NUMERO DE VEZES EM QUE CADA REMEDIO FOI RECEITADO PELOS VETERINARIOS:
CREATE VIEW VW_REMEDIOS
AS
	SELECT RM.REM_ST_NOME AS NOME, COUNT(*) AS TOT_RECEITAS
	FROM VET_VETERINARIO V
	JOIN VET_CONSULTA C ON V.VET_IN_CODIGO = C.VET_IN_CODIGO
	JOIN VET_RECEITA R ON C.CON_IN_ID = R.CON_IN_ID
	JOIN VET_REMEDIO RM ON R.REM_IN_CODIGO = RM.REM_IN_CODIGO
	GROUP BY RM.REM_ST_NOME
GO

-- REMEDIO COM O MENOR NUMERO DE RECEITAS:
SELECT NOME AS Nome
FROM VW_REMEDIOS
WHERE TOT_RECEITAS =
	(SELECT MIN(TOT_RECEITAS) FROM VW_REMEDIOS)
GO
